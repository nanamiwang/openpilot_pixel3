import os
Import('env', 'arch', 'cereal', 'messaging', 'common', 'gpucommon', 'visionipc', 'USE_WEBCAM')

libs = ['m', 'pthread', common, 'jpeg', 'OpenCL', 'yuv', cereal, messaging, 'zmq', 'capnp', 'kj', visionipc, gpucommon]

cameras = []
if arch == "larch64":
  libs += ['atomic']
  cameras = ['cameras/camera_qcom2.cc']
  if os.path.isfile('/data/pixel3'):
    cameras = ['cameras/camera_pixel3.cc']
    env = env.Clone()
    env.Append(CXXFLAGS = '-DPIXEL3')
    env.Append(CFLAGS = '-DPIXEL3')
    if not os.path.isfile('/data/enable_yuv'):
      env.Append(CXXFLAGS = '-DUSE_RAW10')
      env.Append(CFLAGS = '-DUSE_RAW10')

  env.Program('camerad', [
      'main.cc',
      'cameras/camera_common.cc',
      'imgproc/utils.cc',
      cameras,
    ], LIBS=libs)

if GetOption("test"):
  env.Program('test/ae_gray_test', [
      'test/ae_gray_test.cc',
      'cameras/camera_common.cc',
    ], LIBS=libs)
